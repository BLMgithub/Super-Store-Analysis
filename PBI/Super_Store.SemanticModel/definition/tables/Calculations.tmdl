table Calculations
	lineageTag: 2869bfe0-868d-437f-ad1c-587b1890069b

	measure TotalSales = ```
			
			        SUM(FACT_Sales[Sales])
			
			
			```
		displayFolder: _TotalMetrics
		lineageTag: 0d76ebf8-4f44-48e5-bc20-79b6cffa5aa4

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure CYSales = ```
			
			        CALCULATE(
			            'Calculations'[TotalSales],
			            KEEPFILTERS('DIM_Date'[Year] = 2014)
			        )
			
			```
		displayFolder: 0_CYMerics2014
		lineageTag: 428d9d4e-7880-473c-b6ff-71c1535581e9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure PrevYrSales = ```
			
			        CALCULATE(
			            [TotalSales],
			            KEEPFILTERS(DIM_Date[Year] = 2013)
			        )
			
			
			```
		displayFolder: 0_PYMetrics2013
		lineageTag: ada2c66a-b74b-49eb-9c8c-3c7b01862e8d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure TotalOrders = ```
			
			        COUNTROWS('FACT_Sales')
				
			```
		formatString: 0
		displayFolder: _TotalMetrics
		lineageTag: 618655d3-9d1c-4d1e-994c-63ba2d23c746

	measure PrevYrOrders = ```
			
			        CALCULATE(
			            [TotalOrders],
			            KEEPFILTERS(DIM_Date[Year] = 2013)
			        )
			        
			```
		formatString: 0
		displayFolder: 0_PYMetrics2013
		lineageTag: 0d760e1f-18c3-4aca-a52e-13accf9fc6a0

	measure CYOrders = ```
			
			        FORMAT(
			            CALCULATE(
			                [TotalOrders],
			                KEEPFILTERS(DIM_Date[Year] = 2014)
			            ),
			            "#,##0"
			        )
			
			
			```
		displayFolder: 0_CYMerics2014
		lineageTag: f449c2a9-982b-470c-be97-de4775a062c0

	measure TotalQuantity = ```
			
			        SUM(FACT_Sales[Quantity])
				
			```
		formatString: 0
		displayFolder: _TotalMetrics
		lineageTag: f1fdbe3b-30e5-459e-8bd7-30423a016c73

	measure PrevYrQuantity = ```
			
			        CALCULATE(
			            [TotalQuantity],
			            KEEPFILTERS(DIM_Date[Year] = 2013)
			        )
				
			```
		formatString: 0
		displayFolder: 0_PYMetrics2013
		lineageTag: 2ee4d46e-c926-4bb7-ae1b-7115a493afa9

	measure CYQuantity = ```
			
			        FORMAT(
			            CALCULATE(
			                [TotalQuantity],
			                KEEPFILTERS(DIM_Date[Year] = 2014)
			            ),
			            "#,##0"
			        )
			
			
			```
		displayFolder: 0_CYMerics2014
		lineageTag: 54dd178a-3256-42be-9d85-5475e6e4d967

	measure TotalProfit = ```
			
					SUM(FACT_Sales[Profit])
				
			```
		displayFolder: _TotalMetrics
		lineageTag: 8a4cef29-8b57-45bf-8859-fae78a33e9eb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure CYProfit = ```
			
					CALCULATE(
						[TotalProfit],
						KEEPFILTERS(DIM_Date[Year] = 2014)
					)
				
			```
		displayFolder: 0_CYMerics2014
		lineageTag: 74e6f705-3442-44e7-83b2-17fef9d38c22

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure PrevYrProfit = ```
			
			        CALCULATE(
			            [TotalProfit],
			            KEEPFILTERS(DIM_Date[Year] = 2013)
			        )
			
			
			```
		displayFolder: 0_PYMetrics2013
		lineageTag: 46818fb8-0ddf-4899-b6b3-bd9631ad4801

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure ProfitMargin = ```
			
					DIVIDE([CYProfit],[CYSales])
					
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 0_CYMerics2014
		lineageTag: 1b8ab2e1-dc5f-4049-b0b0-ad767b60e099

	measure SalesVsPrevYr = ```
			
					FORMAT(
						DIVIDE(
							([CYSales] - [PrevYrSales]),
							[PrevYrSales]
						),
						"▲ +0.00%; ▼-0.00%"
					)
				
			```
		displayFolder: 1_VsPrevYearMetrics
		lineageTag: c964cff6-8d9e-4299-a3e2-946726e7e419

	measure OrdersVsPrevYr = ```
			
					FORMAT(
						DIVIDE(
							([CYOrders] - [PrevYrOrders]),
							[PrevYrOrders]
						),
						"▲ +0.00%; ▼-0.00%"
					)
				
			```
		displayFolder: 1_VsPrevYearMetrics
		lineageTag: 0c973352-46ba-4df5-ad93-183f38275bf3

	measure ProfitVsPrevYr = ```
			
					FORMAT(
						DIVIDE(
							([CYProfit] - [PrevYrProfit]),
							[PrevYrProfit]
						),
						"▲ +0.00%; ▼-0.00%"
					)
			
				
			```
		displayFolder: 1_VsPrevYearMetrics
		lineageTag: 4e0ad9b6-3311-4980-a25f-37512e44a2ec

	measure QuantityVsPrevYr = ```
			
					FORMAT(
						DIVIDE(
							([CYQuantity] - [PrevYrQuantity]),
							[PrevYrQuantity]
						),
						"▲ +0.00%; ▼-0.00%"
					)
			
			```
		displayFolder: 1_VsPrevYearMetrics
		lineageTag: 5ce0f074-93f3-4e31-8fa3-54a6a8f3ff26

	measure HighPerfSales = ```
			
			        CALCULATE(
			            [CYSales],
			            KEEPFILTERS('DIM_Product'[Performance] = "High")
			        ) + 0 // Returns 0 if Blank
				
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 99553f37-ce4d-4c8d-845e-eee12437789c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Prc_HighPerfSales = ```
			
			        FORMAT(
			            DIVIDE(
			                [HighPerfSales],
			                [CYSales]
			            ),
			            " 0.00%; 0.00%"
			        )
			        
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 10998523-c650-4935-8117-e177fae5768c

	measure ModeratePerfSales = ```
			
			        CALCULATE(
			            [CYSales],
			            KEEPFILTERS('DIM_Product'[Performance] = "Moderate")
			        ) + 0 // Returns 0 if Blank
				
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 8945a0da-69e1-4c4a-9117-9dd89d3f6c53

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Prc_ModeratePerfSales = ```
			
			        FORMAT(
			            DIVIDE(
			                [ModeratePerfSales],
			                [CYSales]
			            ),
			            " 0.00%; 0.00%"
			        )
			
				
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: af1cbfed-3182-4526-9613-6724743730f2

	measure LowPerfSales = ```
			
			        CALCULATE(
			            [CYSales],
			            KEEPFILTERS('DIM_Product'[Performance] = "Low")
			        ) + 0 // Returns 0 if blank
				
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 90040f9e-6b8a-4d47-a790-3f27cdb14e15

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Prc_LowPerfSales = ```
			
			        FORMAT(
			            DIVIDE(
			                [LowPerfSales],
			                [CYSales]
			            ),
			            " 0.00%; 0.00%"
			        )
			
			
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 60e60322-6683-4558-86df-948b890dfbb6

	measure CY_ConsumerSales = ```
			
			        CALCULATE(
			            [CYSales],
			            'DIM_Segment'[Segment] = "Consumer"
			        )
			
			```
		displayFolder: 1_SegmentMetrics
		lineageTag: 338547b3-08c0-4e34-a93a-067ecb89e38a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Prc_ConsumerSales = ```
			
			
			        VAR _PrevYrConsumerSales =
			        CALCULATE(
			            [PrevYrSales],
			            'DIM_Segment'[Segment] = "Consumer"
			        )
			
			        VAR _ConsumerSalesPrc =
			        DIVIDE(
			            [CY_ConsumerSales] - _PrevYrConsumerSales,
			            _PrevYrConsumerSales
			        )
			
			        RETURN
			
			            FORMAT(
			                _ConsumerSalesPrc,
			                "▲ +0.00%; ▼-0.00%"
			            )
			
			
			```
		displayFolder: 1_SegmentMetrics
		lineageTag: 39fbd919-af5c-4d49-8016-79eaf0353c5f

	measure CY_CorporateSales = ```
			
			        CALCULATE(
			            [CYSales],
			            'DIM_Segment'[Segment] = "Corporate"
			        )
			
			
			```
		displayFolder: 1_SegmentMetrics
		lineageTag: fca6db83-b30e-4db9-91b6-9c045fb046e1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Prc_CorporateSales = ```
			
			
			        VAR _PrevYrCorporateSales =
			        CALCULATE(
			            [PrevYrSales],
			            'DIM_Segment'[Segment] = "Corporate"
			        )
			
			        VAR _CorporateSalesPrc =
			        DIVIDE(
			            [CY_CorporateSales] - _PrevYrCorporateSales,
			            _PrevYrCorporateSales
			        )
			
			        RETURN
			
			            FORMAT(
			                _CorporateSalesPrc,
			                "▲ +0.00%; ▼-0.00%"
			            )
			
			
			```
		displayFolder: 1_SegmentMetrics
		lineageTag: ace7db5a-d576-4d0b-906b-1056ecbda7b8

	measure CY_HomeOfficeSales = ```
			
			        CALCULATE(
			            [CYSales],
			            'DIM_Segment'[Segment] = "Home Office"
			        )
			
			        
			```
		displayFolder: 1_SegmentMetrics
		lineageTag: 895ccbbf-0bbe-48ed-83cd-f30d1006f403

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Prc_HomeOfficeSales = ```
			
			
			        VAR _PrevYrHomeOfficeSales =
			        CALCULATE(
			            [PrevYrSales],
			            'DIM_Segment'[Segment] = "Home Office"
			        )
			
			        VAR _HomeOfficeSalesPrc =
			        DIVIDE(
			            [CY_HomeOfficeSales] - _PrevYrHomeOfficeSales,
			            _PrevYrHomeOfficeSales
			        )
			
			        RETURN
			
			            FORMAT(
			                _HomeOfficeSalesPrc,
			                "▲ +0.00%; ▼-0.00%"
			            )
			
			
			
			```
		displayFolder: 1_SegmentMetrics
		lineageTag: f6172fe2-1199-4d19-a34f-454b8f2071df

	measure ProfitMarginLvl = ```
			
					SWITCH(
						TRUE(),
						[ProfitMargin] < 0.05, "Low",
						[ProfitMargin] < 0.15, "Moderate",
						[ProfitMargin] >= 0.15, "Excellent"
					)
			
			```
		displayFolder: 0_CYMerics2014
		lineageTag: d1f1c9ec-d486-4dc3-b4ab-82d9bda92254

	measure Formatter_ConsumerSales = ```
			
			        FORMAT(
			            [CY_ConsumerSales],
			            
			        VAR _ConsumerSales =
			            [CY_ConsumerSales]
			
			        RETURN
			            SWITCH(
			                TRUE(),
			                _ConsumerSales < 1000000, "$#,.00K",
			                _ConsumerSales >= 1000000, "$#,,.00M"
			            )
			        )
			```
		displayFolder: 2_Visual_Segment
		lineageTag: 6951fe92-ca62-4af7-ba66-46eea32435ea

	measure Formatter_CorporateSales = ```
			
			        FORMAT(
			            [CY_CorporateSales],
			            
			        VAR _ConsumerSales =
			            [CY_CorporateSales]
			
			        RETURN
			            SWITCH(
			                TRUE(),
			                _ConsumerSales < 1000000, "$#,.00K",
			                _ConsumerSales >= 1000000, "$#,,.00M"
			            )
			        )
			```
		displayFolder: 2_Visual_Segment
		lineageTag: a7bcc2c3-60f2-4b6d-a7e3-ede3b7093436

	measure Formatter_HomeOfficeSales = ```
			
			        FORMAT(
			            [CY_HomeOfficeSales],
			            
			        VAR _ConsumerSales =
			            [CY_HomeOfficeSales]
			
			        RETURN
			            SWITCH(
			                TRUE(),
			                _ConsumerSales < 1000000, "$#,.00K",
			                _ConsumerSales >= 1000000, "$#,,.00M"
			            )
			        )
			```
		displayFolder: 2_Visual_Segment
		lineageTag: b2b38965-f0e1-4cad-af7b-62cc94f23f63

	measure CY_After_HighSales = ```
			
			    [CYSales] - [HighPerfSales]
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 2253c21e-c2b3-463b-a815-32117d778af2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure CY_After_ModerateSales = ```
			
			    [CYSales] - [ModeratePerfSales]
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: 6d1e5452-58d4-4138-9135-c4543880d24c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure CY_After_LowSales = ```
			
			    [CYSales] - [LowPerfSales]
			```
		displayFolder: 1_ProductPerfMetrics
		lineageTag: c7dc3523-acf6-4b25-9d41-d4a9193b0ad6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Regional_X_Axis_Formatter = ```
			
			        MAXX(
			            ALLSELECTED('DIM_CountryMarket'[Market]),
			            [CYSales]
			            ) * 1.0
			```
		displayFolder: 2_Visual_CYRevTT
		lineageTag: 729f5ec3-aa6c-4351-86ad-46f72f83bad8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure MaxY_Consumer = ```
			
			        CALCULATE(
			            MAXX(
			                ALLSELECTED('DIM_Date'[MonthName]),
			                [CY_ConsumerSales]
			                ) * 1.3,
			                ALLEXCEPT('DIM_Date', 'DIM_Date'[Year])
			        )
			
			```
		formatString: 0
		displayFolder: 2_Visual_Segment
		lineageTag: e5598ebf-c878-441c-a98c-0d90d06dc132

	measure Space_Consumer = ```
			
			        [MaxY_Consumer] - [CY_ConsumerSales]
			```
		displayFolder: 2_Visual_Segment
		lineageTag: 156d7e3b-7921-4340-a19c-0dd3cfbe0e94

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure MaxY_Corporate = ```
			
			        CALCULATE(
			            MAXX(
			                ALLSELECTED('DIM_Date'[MonthName]),
			                [CY_CorporateSales]
			                ) * 1.3,
			                ALLEXCEPT('DIM_Date', 'DIM_Date'[Year])
			        )
			
			```
		displayFolder: 2_Visual_Segment
		lineageTag: abefaf12-1153-4f4b-be7c-545b3b549af1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Space_Corporate = ```
			
			        [MaxY_Corporate] - [CY_CorporateSales]
			```
		displayFolder: 2_Visual_Segment
		lineageTag: bc457c09-d394-465b-b973-aa58d69d1f53

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure MaxY_HomeOffice = ```
			
			        CALCULATE(
			            MAXX(
			                ALLSELECTED('DIM_Date'[MonthName]),
			                [CY_HomeOfficeSales]
			                ) * 1.3,
			                ALLEXCEPT('DIM_Date', 'DIM_Date'[Year])
			        )
			
			```
		displayFolder: 2_Visual_Segment
		lineageTag: 537fd53b-d31b-4f5a-a900-207c80b5fa2e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Space_HomeOffice = ```
			
			        [MaxY_HomeOffice] - [CY_HomeOfficeSales]
			```
		displayFolder: 2_Visual_Segment
		lineageTag: 12901d90-a072-4cb8-850d-67c477c2b1fd

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure MaxDay_X_Axis_Formatter = ```
			
			    VAR _MaxMonthDays =
			        CALCULATE(
			            COUNT('DIM_Date'[Day]),
			            'DIM_Date'[Year] = 2014)
			    
			    VAR _MaxAxisDate =
			        MAXX(
			            ALLSELECTED('DIM_Date'[Day]),
			            _MaxMonthDays
			        )
			
			    RETURN
			        _MaxAxisDate
			```
		formatString: 0
		displayFolder: 2_Visual_CYRevTT
		lineageTag: 65ee5203-b82d-47e9-8ff1-dd69c9d73bdd

	measure MaxY_CYRev = ```
			
			
			// Not working properly with other time filter.
			        // CALCULATE(
			        //     MAXX(
			        //         ALLSELECTED('DIM_Date'[Day]),
			        //         [CYSales]
			        //         ) * 1.5,
			        //         ALLEXCEPT('DIM_Date', 'DIM_Date'[Year])
			        // )
			
			
			// Works perfectly with multiple filter
			        MAXX(
			            ALLSELECTED('DIM_Date'[Day]),
			            [CYSales]
			            ) * 1.5 
			
			```
		displayFolder: 2_Visual_CYRevTT
		lineageTag: a152a535-9ae5-488c-a5d9-8e89dbee59f8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Top3_SubC_Filter = ```
			
			    VAR _ProdSalesRank =
			        RANKX(
			            ALLSELECTED('DIM_Product'[SubCategory]),
			            [CYSales],
			            ,
			            DESC,
			            Dense
			            )
			            
			    VAR _Result =
			        IF(
			            _ProdSalesRank <= 3,
			            1,
			            0
			        )
			
			    RETURN
			        _Result
			```
		formatString: 0
		displayFolder: 2_Visual_ProdCategTT
		lineageTag: 4f04854f-43ff-4b6d-a57d-4d1da35cf5de

	measure ProdRank = ```
			
			        RANKX(
			            ALLSELECTED('DIM_Product'[SubCategory]),
			            [CYSales],
			            ,
			            DESC,
			            Dense
			            )
			```
		formatString: 0
		displayFolder: 2_Visual_ProdCategTT
		lineageTag: 7b3b52dc-aed8-477b-8299-cb4726d925ae

	measure Top3_GT_SalesPrc = ```
			
			    VAR _CategoryTotal = 
			        CALCULATE(
			            [CYSales],
			            ALLEXCEPT(
			                'DIM_Product',
			                'DIM_Product'[Category],
			                'DIM_Product'[Performance]
			                )
			        )
			    
			    VAR _Result =
			        DIVIDE(
			            [CYSales],
			            _CategoryTotal,
			            0
			        )
			
			    RETURN
			        _Result
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2_Visual_ProdCategTT
		lineageTag: 8dfa327b-8ed6-41cc-97c0-ebd55f44f899

	measure ProdCateg_X_Axis_Formatter = ```
			
			        MAXX(
			            ALLSELECTED('DIM_Product'[Category]),
			            [CYSales]
			            ) * 1.0
			```
		displayFolder: 2_Visual_CYRevTT
		lineageTag: cc165bf7-7b5b-4b9a-b48e-862b04dc81f7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Bars_CYSales = ```
			
			        [CYSales]
			
			```
		displayFolder: 2_Visual_ProdCategTT
		lineageTag: c29fbca3-c8cd-4e05-aec6-cab53c782acd

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure CountryRank = ```
			
			        RANKX(
			            ALLSELECTED('DIM_CountryMarket'[CountryName]),
			            [CYSales],
			            ,
			            DESC,
			            Dense
			            )
			```
		formatString: 0
		displayFolder: 2_VisualRegionTT
		lineageTag: d677cdd1-f051-4c02-8081-7ef93816e814

	measure Top5_GT_SalesPrc = ```
			
			    VAR _MarketTotal = 
			        CALCULATE(
			            [CYSales],
			            ALLEXCEPT(
			                'DIM_CountryMarket',
			                'DIM_CountryMarket'[Market]
			                ),
			            ALLEXCEPT(
			                'DIM_Segment',
			                'DIM_Segment'[Segment]
			            )
			        )
			    
			    VAR _Result =
			        DIVIDE(
			            [CYSales],
			            _MarketTotal,
			            0
			        )
			
			    RETURN
			        _Result
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 2_VisualRegionTT
		lineageTag: a0addea3-e1cb-4b59-8a9c-597ae436f3c7

	measure Top5_Country_Filter = ```
			
			    VAR _CountrySalesRank =
			        RANKX(
			            ALLSELECTED('DIM_CountryMarket'[CountryName]),
			            [CYSales],
			            ,
			            DESC,
			            Dense
			            )
			            
			    VAR _Result =
			        IF(
			            _CountrySalesRank <= 5,
			            1,
			            0
			        )
			
			    RETURN
			        _Result
			```
		formatString: 0
		displayFolder: 2_VisualRegionTT
		lineageTag: 2bbccb4d-f2a8-4891-bcb9-df6258260628

	measure Bars__CYSales = ```
			
			        [CYSales]
			```
		displayFolder: 2_VisualRegionTT
		lineageTag: 009eb9a4-7d57-404a-88bf-fd2de19bf6f1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Chart_title_1 = ```
			
			        "‎ ‎ ‎ Category: " & SELECTEDVALUE('DIM_Product'[Category]) 
			            & " ‎ ‎ ‎ | ‎ ‎ ‎ " &
			                "Performance: "& SELECTEDVALUE('DIM_Product'[Performance])
			```
		displayFolder: 2_Visual_ProdCategTT
		lineageTag: 065a7919-f132-46db-954a-33b69dfd2f60

	measure Chart_title_2 = ```
			
			        "‎ ‎ ‎ Market : " & SELECTEDVALUE('DIM_CountryMarket'[Market]) 
			            & " ‎ ‎ ‎ | ‎ ‎ ‎ " &
			                "Customer Segment : "& SELECTEDVALUE('DIM_Segment'[Segment])
			```
		displayFolder: 2_VisualRegionTT
		lineageTag: 0be67853-23a0-4c82-b6f1-7880a8852a02

	column Value
		isHidden
		formatString: 0
		lineageTag: 1b880905-67d1-4620-b3aa-ea9f9752923c
		summarizeBy: sum
		isNameInferred
		sourceColumn: [Value]

		annotation SummarizationSetBy = Automatic

	partition Calculations = calculated
		mode: import
		source = Row("Value", BLANK())

	annotation PBI_Id = 391e5cf729de4ee2b6e1e432dbddd628

	annotation 436ba87b-9c83-4389-a31b-ebd06a36be98 = {"Expression":""}

